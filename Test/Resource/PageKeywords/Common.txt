*** Settings ***
Resource          ../../../../ArcadiaAtlasRobot/Common/Keywords/RedefindKeywords.txt
Resource          ../../../../ArcadiaAtlasRobot/Common/Keywords/RunOnFailKeywords.txt
Resource          ../PageRepository/AdidasIndonesiaRepository.txt
Resource          ../PageVariable/AdidasIndonesiaCommonVariable.txt
Resource          ../PageRepository/CartRepository.txt
Resource          ../Localized/${ar_LANG}/CartLocalized.txt
Resource          ../../../../Config/LocalConfig.txt
Resource          ../Localized/${ar_LANG}/AdidasIndonesiaCommonLocalized.txt

*** Keywords ***
Open BrowserStack
    [Arguments]    ${Username}    ${AccessKey}    ${version}
    Open Browser    ${TEST_BASE_URL}    ${ar_BROWSER_STACK}    remote_url=http://${Username}:${AccessKey}@hub.browserstack.com/wd/hub    desired_capabilities=os:devices,os_version:${version},browserstack.debug:true

Keywords For Teardown
    Run Keyword If Test Failed    Capture Screen with Specific name
    Run Keyword And Continue On Failure    Close Browser

Search Product
    Run Keyword If    "${ar_testType}"=="device"    Click Web Element    ${btnSearchProduct}
    Input Web Text    ${home_search_textarea}    ${txtShoes}
    Press Enter

Press Enter
    Press Key    ${home_search_textarea}    \\13

Open Browser And Maximize
    Open Browser    ${TEST_BASE_URL}    ${ar_BROWSER}
    Maximize Browser Window

Check Promotion Popup
    ${POPStatus}=    Run Keyword And Return Status    Web Element Should Be Visible    xpath=//*[@id="mrNlPopupClose"]    5s
    log    ${POPStatus}
    Run Keyword If    '${POPStatus}'=='True'    Click Web Element    xpath=//*[@id="mrNlPopupClose"]

Select Menu
    [Arguments]    ${arg1}    ${arg2}    ${arg3}
    Run Keyword If    '${ar_testType}'=='web'    Select Menu By Web    ${arg1}    ${arg2}    ${arg3}
    Run Keyword If    '${ar_testType}'=='device'    Select Menu By Mobile    ${arg1}    ${arg2}    ${arg3}

Select Menu By Mobile
    [Arguments]    ${arg1}    ${arg2}    ${arg3}
    Click Web Element    ${btnMenuBar_Mobile}
    Click Web Element    ${mnu_${arg1}}
    Click Web Element    ${subMnu_${arg1}_${arg2}}
    Click Web Element    ${subMnu_${arg1}_${arg2}_${arg3}}

Select Menu By Web
    [Arguments]    ${arg1}    ${arg2}    ${arg3}
    Wait Web Until Page Element Visible    ${menuBar}
    Mouse Over    ${mnu_${arg1}}
    Click Web Element    ${subMnu_${arg1}_${arg2}_${arg3}}

Wait For Loading Page Success
    ${state}=    Run Keyword And Return Status    Selenium2Library.Wait Until Element Is Visible    ${elementLoadingPage}    15s
    Run Keyword If    '${state}'=='True'    Selenium2Library.Wait Until Element Is Not Visible    ${elementLoadingPage}    15s

get number total product
    ${text}    Get Web Text    ${txtTotalProduct}
    ${text}    Replace String    ${text}    (    ${EMPTY}
    ${text}    Replace String    ${text}    )    ${EMPTY}
    @{total}    Split String    ${text}
    ${totalProduct}    Convert To Integer    @{total}[0]
    [Return]    ${totalProduct}

get price product
    [Arguments]    ${locator}
    ${secondPrice}    Get Web Text    ${locator}
    @{txtPrice}    Split String    ${secondPrice}
    ${price}    Replace String    @{txtPrice}[1]    .    ${EMPTY}
    [Return]    ${price}

Select Login / Register
    Run Keyword If    "${ar_testType}"=="web"    login on web
    Run Keyword If    "${ar_testType}"=="device"    log in on browserstack

log in on browserstack
    Check Promotion Popup
    Click Web Element    ${navbar_Mobile}
    Click Web Element    ${mnu_Login-Logout_Mobile}
    Check Promotion Popup

login on web
    Check Promotion Popup
    Click Web Element    ${mnu_Login-Logout}
    Check Promotion Popup

Sign In
    [Arguments]    ${email}    ${password}
    Select Login / Register
    Focus    ${formLogin}
    Input Web Text    ${txtInputEmail}    ${email}
    Input Web Text    ${txtInputPassword}    ${password}
    Click Web Element    ${btnLogin}

Get Number Color Product
    [Arguments]    ${txtColor_Product}
    ${number}    Get Web Text    ${txtColor_Product}
    @{text}    Split String    ${number}
    ${length}    Get Length    ${text}
    ${number_color}    Run Keyword If    ${length}==0    Set Variable    0
    ...    ELSE    Set Variable    @{text}[0]
    [Return]    ${number_color}

Get Name Picture Product
    [Arguments]    ${img_Product}
    ${src_pic}    Get Element Attribute    ${img_Product}@src
    @{name_pic}    Split String    ${src_pic}    /
    ${lastIndex}    Get Length    ${name_pic}
    ${lastIndex}    Evaluate    ${lastIndex}-1
    ${name_pic}    Set Variable    @{name_pic}[${lastIndex}]
    [Return]    ${name_pic}

Logout
    Run Keyword If    "${ar_testType}"=="web"    logout on web
    Run Keyword If    "${ar_testType}"=="device"    logout on browserstack

logout on web
    Click Web Element    ${mnu_Login-Logout}
    Click Web Element    ${subMnu_Logout}
    Web Element Text Should Be    ${lblCheckLogout}    ${txtCheckLogout}

logout on browserstack
    Check Promotion Popup
    Click Web Element    ${navbar_Mobile}
    Click Web Element    ${mnu_Login-Logout_Mobile}
    Check Promotion Popup

get number quantity per page
    ${val_itemqty_per_page}    Get Text    css=label.active
    ${val_itemqty_per_page}    Convert To Integer    ${val_itemqty_per_page}
    [Return]    ${val_itemqty_per_page}

Search Product Shirts
    Run Keyword If    "${ar_testType}"=="device"    Click Web Element    ${btnSearchProduct}
    Input Web Text    ${home_search_textarea}    ${txtShirt}
    Press Enter

Set Evironment Test On Staging
    [Arguments]    ${ar_testType}
    Run Keyword If    "${ar_testType}"=="web"    Open Browser And Maximize
    Run Keyword If    "${ar_testType}"=="device"    Open BrowserStack    ${Username}    ${AccessKey}    ${version}
